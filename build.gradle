plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'com.enonic.defaults' version '2.1.5'
    id 'com.enonic.xp.base' version '3.4.0'
    id 'org.asciidoctor.jvm.convert' version '4.0.2'
    id "org.aim42.htmlSanityCheck" version '1.1.6'
}

dependencies {
    compileOnly "com.enonic.xp:portal-api:${xpVersion}"
    compileOnly "com.enonic.xp:script-api:${xpVersion}"

    implementation "com.enonic.xp:lib-auth:${xpVersion}"
    implementation "com.enonic.xp:lib-content:${xpVersion}"
    implementation "com.enonic.xp:lib-context:${xpVersion}"
    implementation "com.enonic.xp:lib-event:${xpVersion}"
    implementation "com.enonic.xp:lib-node:${xpVersion}"
    implementation "com.enonic.xp:lib-portal:${xpVersion}"
    implementation "com.enonic.xp:lib-common:${xpVersion}"
    implementation "com.enonic.xp:lib-websocket:${xpVersion}"
    implementation 'com.enonic.lib:lib-graphql:2.1.0'

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.10.0'
    testImplementation "com.enonic.xp:testing:${xpVersion}"
}

repositories {
    mavenLocal()
    mavenCentral()
    xp.enonicRepo( 'dev' )
}

jacocoTestReport {
    reports {
        xml.required = true
        html.required = true
    }
}

check.dependsOn jacocoTestReport

test {
    useJUnitPlatform()
}

def docsDir = file( "${buildDir}/docs" )

asciidoctor {
    sourceDir = file( 'docs' )
    outputDir = docsDir

    attributes 'source-highlighter': 'coderay'

    resources {
        from( sourceDir ) {
            include '**/images/**'
            include '**/media/**'
        }
    }
}

htmlSanityCheck {
    dependsOn asciidoctor

    sourceDir = docsDir
}
